
use v6.d;
use lib 'lib';
use ORM::ActiveRecord::Model;
use Test;

class Page {...} # stub

class User is Model is export {
  submethod BUILD {
    self.has-many: pages => class => Page;

    self.validate: 'fname', { :presence, length => { min => 4, max => 32 } }
    self.validate: 'lname', { :presence, length => { min => 4, max => 32 } }
  }

  method fullname {
    self.fname ~ ' ' ~ self.lname;
  }
}

class Page is Model is export {
  submethod BUILD {
    self.belongs-to: user => class => User;

    self.validate: 'name', { :presence, length => { min => 4, max => 32 } }
  }
}

plan 5;

%*ENV<DISABLE-SQL-LOG> = True;

my User $user = User.create({fname => 'Greg', lname => 'Donald'});
ok $user.id;

Page.create({:$user, name => 'Raku'});

my Page $page = $user.pages.first;
ok $page.name eq 'Raku';
ok $page.user.fullname eq 'Greg Donald';

my User $alfred = User.create({fname => 'Alfred E.', lname => 'Neuman'});
ok $alfred.id;

$page.update({user => $alfred});
ok $page.user.fullname eq 'Alfred E. Neuman';
